# Original maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Modified for UBOS: Johannes Ernst <jernst@indiecomputing.com>
# * add releasepage
# * added pkgverforked
# * do not install .ini file so UBOS only activates it if needed

developer=http://php.net/
maintainer=http://indiecomputing.com/
pkgname=$(basename $(pwd))
_extname=oauth
pkgver=2.0.5
pkgrel=1
pkgdesc="PHP extension to provide OAuth consumer and provider bindings."
arch=( 'any' )
url="http://www.php.net/manual/en/intro.oauth.php"
license=('BSD')
depends=('php')
source=("http://pecl.php.net/get/$_extname-$pkgver.tgz")
sha512sums=('847f8a07aba930d606038a78915ddd9c24aab241d27b844a766e0da3caabf419fd891573524a55b40559a827b1adc1261748efabac5ed1bee45fa770935379ee')
backup=("etc/php/conf.d/$_extname.ini")

releasepage=('https://aur.archlinux.org/cgit/aur.git/tree/PKGBUILD?h=php-oauth')
pkgverforked=('2.0.5-1')

build() {
    cd "$srcdir/$_extname-$pkgver"

    phpize
    ./configure
    make
}

package() {
    cd "$srcdir/$_extname-$pkgver"
    make INSTALL_ROOT="$pkgdir" install

    install -m0644 -D "LICENSE" "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"

    install -m0755 -d "$pkgdir/etc/php/conf.d/"
    # echo "extension=$_extname.so" > "$pkgdir/etc/php/conf.d/$_extname.ini"
    # chmod 0644 "$pkgdir/etc/php/conf.d/$_extname.ini"
}
